plugins{
    id 'application'
    id 'org.openjfx.javafxplugin' version '0.0.8'
    id "net.ltgt.apt" version "0.21"
}

mainClassName = 'edu.unm.dragonfly.DashboardApplication'

group 'edu.unm.dragonfly'
version '1.0.0-SNAPSHOT'

java{
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
}

javafx {
    version = "12"
    modules = [ 'javafx.controls', 'javafx.fxml', 'javafx.swing' ]
}

compileJava.options.encoding = 'UTF-8'

repositories {
    jcenter()
    mavenLocal()
    mavenCentral()
    maven {
        url 'https://esri.jfrog.io/artifactory/arcgis'
    }
}

ext {
    arcgisVersion = '100.10.0'
}

configurations {
    natives
}

dependencies {

    compile "com.esri.arcgisruntime:arcgis-java:$arcgisVersion"
    natives "com.esri.arcgisruntime:arcgis-java-jnilibs:$arcgisVersion"
    natives "com.esri.arcgisruntime:arcgis-java-resources:$arcgisVersion"

    compile 'com.google.inject:guice:4.2.3'
    annotationProcessor 'com.google.auto.value:auto-value:1.7.5'
    compile 'com.google.auto.value:auto-value-annotations:1.7.5'

    compile 'io.reactivex.rxjava2:rxjava:2.2.19'
    compile 'io.reactivex.rxjava2:rxjavafx:2.2.2'

    compile 'org.controlsfx:controlsfx:11.0.3'

    compile 'edu.brown.cs.burlap:java_rosbridge:2.0.3-SNAPSHOT'

    testImplementation 'junit:junit:4.13'
}

task copyNatives(type: Copy) {
    description = "Copies the arcgis native libraries into the project build directory for development."
    group = "build"
    configurations.natives.asFileTree.each {
        from(zipTree(it))
    }
    // store native libraries in a common location shared with other samples
    into "${System.properties.getProperty("user.home")}/.arcgis/$arcgisVersion"
}

build {
    dependsOn copyNatives
}